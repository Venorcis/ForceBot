use "../BattleMind.act2g" as actionspec.
use "../BattleMind.pl" as knowledge.
use UpdateRegionOrder as module.
order = linear.

/**
* Determines what units in each region should do.
*/
module AttackFlowsContinue(Source, Target) {
	% If there are enemies present
	if bel((not(enemySim(Source, 0, 0, 0, 0, 0)) ; not(enemySim(Target, 0, 0, 0, 0, 0))), combatSim(Source, FHealth, FRemainder, EHealth, ERemainder)) then {
		% Set the order to fight if necessary and continue the loop
		if bel(FRemainder * 2 > FHealth ; ownBase(_, _, Source)) then
				UpdateRegionOrder(Source, Target, 'fight').
				
		% If we would lose, or if our initial assessment isn't fighting, retreat
		if bel((ERemainder * 4 > EHealth ; regionOrder(Source, Target, 'advance')), ownMain(_, _, RetreatId),
			not(regionOrder(Source, RetreatId, 'retreat')), regionOrder(Source, OldTarget, OldOrder)) then
				delete(regionOrder(Source, OldTarget, OldOrder)) +
				insert(regionOrder(Source, RetreatId, 'retreat')) +
				(fighterRegionOrders).send:(regionOrder(Source, RetreatId, 'retreat')).
	}
	
	% If there are no enemies, set the order to advance
	if bel(not(regionOrder(Source, Target, 'advance')), regionOrder(Source, OldTarget, OldOrder)) then
			delete(regionOrder(Source, OldTarget, OldOrder)) +
			insert(regionOrder(Source, Target, 'advance')) +
			(fighterRegionOrders).send:(regionOrder(Source, Target, 'advance')).
}
